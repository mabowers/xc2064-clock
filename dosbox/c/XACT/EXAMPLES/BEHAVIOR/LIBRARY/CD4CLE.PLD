TITLE           Counter: BCD, up, 4-bit, async-clear, loadable
PATTERN         vmh
REVISION        5.0.0
AUTHOR          Patrick Phung
COMPANY         Xilinx EPLD
DATE            8/31/93

CHIP            CD4CLE  COMPONENT
 
;Inputs
d0 d1 d2 d3 l ce c clr

; d[3:0]        parallel-load data
; l             parallel-load enable
; ce            count enable
; c             clock (optional FastCLK)
; clr           async clear
 
;Outputs
q0 q1 q2 q3 ceo tc

; q[3:0]        counter output
; ceo           chip enable output (optimizable AND)
; tc            terminal count (optimizable AND)

NODE (UIM) ceo tc

EQUATIONS 

tc       = q0*/q1*/q2*q3

ceo      = ce*q0*/q1*/q2*q3

q3.d1    = ce*/l*q0*q1*q2*/q3
         + ce*/l*/q0*/q1*/q2*q3
q3.d2    =  l*d3
q3.fbk   = /ce*/l
q3      := q3.d1 or q3.d2
q3.clkf  = c
q3.rstf  = clr

q2.d1    = ce*/l*q0*q1*/q2*/q3
         + ce*/l*/q0*q2*/q3
         + ce*/l*/q1*q2*/q3
q2.d2    =  l*d2
q2.fbk   = /ce*/l
q2      := q2.d1 or q2.d2
q2.clkf  = c
q2.rstf  = clr

q1.d1    = ce*/l*q0*/q1*/q3
         + ce*/l*/q0*q1*/q3
q1.d2    =  l*d1
q1.fbk   = /ce*/l
q1      := q1.d1 or q1.d2
q1.clkf  = c
q1.rstf  = clr

q0.d1    = ce*/l*/q0*/q3
         + ce*/l*/q0*/q1*/q2
q0.d2    =  l*d0
q0.fbk   = /ce*/l
q0      := q0.d1 or q0.d2
q0.clkf  = c
q0.rstf  = clr

q0.prld  = gnd
q1.prld  = gnd
q2.prld  = gnd
q3.prld  = gnd
