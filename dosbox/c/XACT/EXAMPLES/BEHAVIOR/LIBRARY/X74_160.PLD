TITLE           4-BIT BCD UP COUNTER, LOAD, ASYNC CLR
REVISION        vmh
AUTHOR          Patrick Phung 
COMPANY         XILINX
DATE            9/21/93
CHIP            X74_160  COMPONENT
  
;INPUTS
ck /load ent enp /clr a b c d
  
; ck:           CLOCK SOURCE  
; /load:        PARALLEL-load CONTROL (ACTIVE-LOW)  
; ent:          CARRY-IN FROM RCO OF PRIOR STAGE  
; enp:          COUNT-ENABLE (PARALLEL TO ALL STAGES)  
; /clr:         ASYNCHRONOUS CLEAR (ACTIVE-LOW)  
; a - d:        PARALLEL-LOAD DATA (d = MSB)  
  
;NODES
ce

;OUTPUTS  
qa qb qc qd  rco
  
; qa-qd:        COUNTER DATA OUTPUT (qd = MSB)  
; rco:          SYNCHRONOUS CARRY-OUT
  
NODE (UIM) ce

EQUATIONS  
  
ce    = enp*ent

qa.d1   = /qa*/qd*ce*/load +/qa*/qb*/qc*ce*/load
qa.d2   = a*load
qa.fbk  = /ce*/load  
qa     := qa.d1 OR qa.d2  
qa.clkf = ck 
qa.rstf = clr  
  
qb.d1   = qa*/qb*/qd*ce*/load  
        + /qa*qb*/qd*ce*/load
qb.d2   = b*load  
qb.fbk  = /ce*/load  
qb     := qb.d1 OR qb.d2  
qb.clkf = ck 
qb.rstf = clr  
  
qc.d1   = qa*qb*/qc*/qd*ce*/load  
        + /qa*qc*/qd*ce*/load
        + /qb*qc*/qd*ce*/load
qc.d2   = c*load  
qc.fbk  = /ce*/load  
qc     := qc.d1 OR qc.d2  
qc.clkf = ck 
qc.rstf = clr  
  
qd.d1   = qa*qb*qc*/qd*ce*/load  
        + /qa*/qb*/qc*qd*ce*/load
qd.d2   = d*load  
qd.fbk  = /ce*/load  
qd     := qd.d1 OR qd.d2  
qd.clkf = ck 
qd.rstf = clr  

rco = qa*/qb*/qc*qd*ent
