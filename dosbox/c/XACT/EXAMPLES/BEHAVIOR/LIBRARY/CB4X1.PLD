TITLE           Counter: binary, 4-bit, async-clear, loadable, up/down
PATTERN         vmh
REVISION        5.0.0
AUTHOR          Patrick Phung
COMPANY         Xilinx EPLD
DATE            8/24/93

CHIP            CB4X1  COMPONENT
 
;Inputs
d0 d1 d2 d3 ceu ced l c clr

; d[3:0]        parallel-load data
; ceu           count-up enable
; ced           count-down enable
; l             parallel-load enable
; c             clock (optional FastCLK)
; clr           async clear
 
;Outputs
q0 q1 q2 q3 tcu tcd ceou ceod

; q[3:0]        counter output
; tcu           terminal count-up (optimizable AND)
; tcd           terminal count-down
; ceou          chip enable output up (optimizable AND)
; ceod          chip enable output down (optimizable AND)

NODE (UIM) tcu ceou ceod

EQUATIONS 

tcu      = q0*q1*q2*q3

tcd     := ceu*/l*q0*q1*q2*q3
         + ced*/ceu*/l*q0*/q1*/q2*/q3
         + l*/d0*/d1*/d2*/d3
         + /ceu*/ced*/l*/q0*/q1*/q2*/q3
tcd.setf = clr
tcd.clkf = c

ceou     = ceu*q0*q1*q2*q3

ceod     = ced*tcd

q3.d1    = ceu*/l*q0*q1*q2
         + ced*/ceu*/l*/q0*/q1*/q2
q3.d2    = l*d3
q3.fbk   = /l
q3      := q3.d1 xor q3.d2
q3.clkf  = c
q3.rstf  = clr

q2.d1    = ceu*/l*q0*q1
         + ced*/ceu*/l*/q0*/q1
q2.d2    = l*d2
q2.fbk   = /l
q2      := q2.d1 xor q2.d2
q2.clkf  = c
q2.rstf  = clr

q1.d1    = ceu*/l*q0
         + ced*/ceu*/l*/q0
q1.d2    = l*d1
q1.fbk   = /l
q1      := q1.d1 xor q1.d2
q1.clkf  = c
q1.rstf  = clr

q0.d1    = ceu*/l
         + ced*/ceu*/l
q0.d2    = l*d0
q0.fbk   = /l
q0      := q0.d1 xor q0.d2
q0.clkf  = c
q0.rstf  = clr

q0.prld  = gnd
q1.prld  = gnd
q2.prld  = gnd
q3.prld  = gnd
tcd.prld  = vcc
