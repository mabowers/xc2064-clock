TITLE           Shift Register: 4-bit, left/right, async-clear, loadable
PATTERN         vmh
REVISION        5.0.0
AUTHOR          Patrick Phung
COMPANY         Xilinx EPLD
DATE            7/30/93

CHIP            SR4CLED  COMPONENT
 
;Inputs
d0 d1 d2 d3 sli sri l left ce c clr

; d[3:0]        parallel-load data
; sli           shift-left serial input
; sri           shift-right serial input
; l             parallel-load enable
; left          shift direction: 1=left, 0=right
; ce            shift clock enable
; c             clock (optional FastCLK)
; clr           async clear
 
;Outputs
q0 q1 q2 q3

; q[3:0]        counter output

EQUATIONS 

q3      := left*ce*/l*q2
         + /left*ce*/l*sri
         + /ce*/l*q3 
         + l*d3 
q3.clkf  = c
q3.rstf  = clr

q2      := left*ce*/l*q1
         + /left*ce*/l*q3
         + /ce*/l*q2 
         + l*d2 
q2.clkf  = c
q2.rstf  = clr

q1      := left*ce*/l*q0
         + /left*ce*/l*q2
         + /ce*/l*q1 
         + l*d1 
q1.clkf  = c
q1.rstf  = clr

q0      := left*ce*/l*sli
         + /left*ce*/l*q1
         + /ce*/l*q0 
         + l*d0 
q0.clkf  = c
q0.rstf  = clr
