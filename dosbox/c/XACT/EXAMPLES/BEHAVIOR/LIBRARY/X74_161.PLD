TITLE           4-BIT BINARY UP COUNTER, LOAD, ASYNC CLR
REVISION        vmh
AUTHOR          JEFF SELTZER  
COMPANY         XILINX
DATE            9/15/93
CHIP            X74_161  COMPONENT
  
;INPUTS
CK /LOAD ENT ENP /CLR A B C D
  
; CK:           CLOCK SOURCE  
; /LOAD:        PARALLEL-LOAD CONTROL (ACTIVE-LOW)  
; ENT:          CARRY-IN FROM SCO OF PRIOR STAGE  
; ENP:          COUNT-ENABLE (PARALLEL TO ALL STAGES)  
; /CLR:         ASYNCHRONOUS CLEAR (ACTIVE-LOW)  
; A - D:        PARALLEL-LOAD DATA (D = MSB)  
  
;OUTPUTS  
QA QB QC QD  RCO
  
; QA-QD:        COUNTER DATA OUTPUT (QD = MSB)  
; RCO:          SYNCHRONOUS CARRY-OUT
  
EQUATIONS  
  
QA.D1 = QA*/LOAD + A*LOAD  
QA.D2 = ENP*ENT*/LOAD  
QA := QA.D1 XOR QA.D2  
QA.CLKF = CK 
QA.RSTF = CLR  
  
QB.D1 = QB*/LOAD + B*LOAD  
QB.D2 = QA*ENP*ENT*/LOAD  
QB := QB.D1 XOR QB.D2  
QB.CLKF = CK 
QB.RSTF = CLR  
  
QC.D1 = QC*/LOAD + C*LOAD  
QC.D2 = QA*QB*ENP*ENT*/LOAD  
QC := QC.D1 XOR QC.D2  
QC.CLKF = CK 
QC.RSTF = CLR  
  
QD.D1 = QD*/LOAD + D*LOAD  
QD.D2 = QA*QB*QC*ENP*ENT*/LOAD  
QD := QD.D1 XOR QD.D2  
QD.CLKF = CK 
QD.RSTF = CLR  

RCO = QA*QB*QC*QD*ENT
